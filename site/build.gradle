plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-android-extensions'
}

def keystoreProperties = new Properties()
if (new File("keystore.properties").exists()) {
    keystoreProperties.load(new FileInputStream(rootProject.file("keystore.properties")))
} else {
    keystoreProperties.load(new FileInputStream(rootProject.file("fakekeystore.properties")))
}

android {
    signingConfigs {
        release {
            keyAlias keystoreProperties['release.key.alias']
            keyPassword keystoreProperties['release.key.password']
            storeFile file(keystoreProperties['release.store.file'])
            storePassword keystoreProperties['release.store.password']
            v1SigningEnabled false
            v2SigningEnabled true
        }
    }

    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "net.ivpn.client"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 115
        versionName "2.8.4"

        manifestPlaceholders = [SENTRY_DSN: keystoreProperties['sentry.dsn']]
        buildConfigField "String", "BILLING_PUBLIC_KEY", keystoreProperties['billing.public.key']

        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            matchingFallbacks = ['release']
        }
        debug {
            signingConfig signingConfigs.debug
            matchingFallbacks = ['debug']
        }
    }

    flavorDimensions "api"
    productFlavors {
        production {
            dimension "api"
            buildConfigField "String", "API_TYPE", "\"production\""
            buildConfigField "String", "BASE_URL", "\"api.ivpn.net\""
        }
        stage {
            dimension "api"
            buildConfigField "String", "API_TYPE", "\"stage\""
            buildConfigField "String", "BASE_URL", keystoreProperties['staging.url']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    dataBinding {
        enabled = true
    }

    androidExtensions {
        experimental = true
    }

    dexOptions {
        incremental true
        javaMaxHeapSize "4g"
    }
}

kapt {
    generateStubs = true
}

dependencies {
    implementation project(path: ':core')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.4.0'

    // JSON Parsing
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.2'

    //    Dagger 2
    kapt 'com.google.dagger:dagger-compiler:2.27'
    implementation 'com.google.dagger:dagger:2.27'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.27'

    //Powerful library for logging
    implementation 'org.slf4j:slf4j-api:1.7.24'
    implementation 'org.slf4j:jul-to-slf4j:1.7.0'
    implementation 'com.github.tony19:logback-android:2.0.0'

    //Navigation component
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    //Navigation component dynamic feature module support
    implementation "androidx.navigation:navigation-dynamic-features-fragment:$nav_version"

    //For crash logging.
    implementation 'io.sentry:sentry-android:4.3.0'

    implementation "androidx.multidex:multidex:2.0.1"

    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}